plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.3'
	id 'io.spring.dependency-management' version '1.1.7'
}

bootJar {
	enabled = false
}

repositories {
	mavenCentral()
}

allprojects {
	group = 'com.pepponechoi'
	version = '0.0.1-SNAPSHOT'

	java {
		toolchain {
			languageVersion = JavaLanguageVersion.of(21)
		}
	}
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'java-library'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'

	configurations {
		compileOnly {
			extendsFrom annotationProcessor
		}
	}

	repositories {
		mavenCentral()
	}

	dependencies {
		implementation 'org.springframework.boot:spring-boot-starter'
		compileOnly 'org.projectlombok:lombok'
		annotationProcessor 'org.projectlombok:lombok'
		testImplementation 'org.springframework.boot:spring-boot-starter-test'
		testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	}

	tasks.named('test') {
		useJUnitPlatform()
	}
}

project(":module-common") {
	dependencies {
		runtimeOnly 'com.mysql:mysql-connector-j'
		implementation 'org.springframework.boot:spring-boot-starter-web'
		implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

		// Spring boot 3.x이상에서 QueryDsl 패키지를 정의하는 방법
		implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
		annotationProcessor "com.querydsl:querydsl-apt:5.0.0:jakarta"
		annotationProcessor "jakarta.annotation:jakarta.annotation-api"
		annotationProcessor "jakarta.persistence:jakarta.persistence-api"
	}

	bootJar {
		enabled = false
	}
}

project(":module-api") {
	dependencies {
		implementation 'org.springframework.boot:spring-boot-starter-web'
		implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
		annotationProcessor "jakarta.persistence:jakarta.persistence-api"
		implementation project(":module-domain")
		implementation project(":module-common")
	}

	bootJar {
		enabled = false
	}
}

project(":module-application") {

	bootJar {
		enabled = true
	}

	jar {
		enabled = false
	}

	dependencies {
		implementation project(":module-api")
		implementation project(":module-common")
		implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
		annotationProcessor "jakarta.persistence:jakarta.persistence-api"
	}
}

project(":module-domain") {

	dependencies {
		implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
		implementation project(":module-common")
		// Spring boot 3.x이상에서 QueryDsl 패키지를 정의하는 방법
		implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
		annotationProcessor "com.querydsl:querydsl-apt:5.0.0:jakarta"
		annotationProcessor "jakarta.annotation:jakarta.annotation-api"
		annotationProcessor "jakarta.persistence:jakarta.persistence-api"

	}

	bootJar {
		enabled = false
	}

	// QueryDsl 빌드 옵션 (선택)
	def querydslDir = "$buildDir/generated/querydsl"

	sourceSets {
		main.java.srcDirs += [ querydslDir ]
	}

	tasks.withType(JavaCompile) {
		options.annotationProcessorGeneratedSourcesDirectory = file(querydslDir)
	}

	clean.doLast {
		file(querydslDir).deleteDir()
	}
}

